plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 11
        versionName "6.1.5.1"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${archivesBaseName}-${defaultConfig.versionName}.aar"
        }
    }

    // build a jar with source files
    task sourcesJarBuild(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadocBuild(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }

    // build a jar with javadoc
    task javadocJarBuild(type: Jar, dependsOn: javadocBuild) {
        classifier = 'javadocBuild'
        from javadocBuild.destinationDir
    }

    artifacts {
        archives sourcesJarBuild
        archives javadocJarBuild
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    api('com.mopub:mopub-sdk-fullscreen:5.16.0@aar') {
        transitive = true
    }

    api 'com.google.android.gms:play-services-ads-identifier:17.0.0'
    api 'com.pollfish:pollfish-googleplay:6.1.5'
}

apply from: '../publish.gradle'